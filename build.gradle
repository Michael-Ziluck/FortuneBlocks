plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.ziluck'
version = '2.2.0-SNAPSHOT'
description = 'Spigot plugin for enabling the Fortune enchantment on any block'

archivesBaseName = 'fortuneblocks'

sourceCompatibility = '1.8'

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // spigot
    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.14.4-R0.1-SNAPSHOT'

    // flyway
    compile group: 'org.flywaydb', name: 'flyway-core', version: '6.0.+'

    // mybatis
    compile group: 'org.mybatis', name: 'mybatis', version: '3.5.+'

    // sql drivers
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.+'

    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.0'

    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.0'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.0'

    testCompile group: 'org.json', name: 'json', version: '20190722'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.6'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'fortuneblocks'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = rootProject.name
                description = rootProject.description
                url = "https://www.spigotmc.org/resources/fortuneblocks.8766/"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mzilu'
                        name = 'Michael Ziluck'
                        email = 'michael@ziluck.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/ZiluckMichael/FortuneBlocks.git'
                    developerConnection = 'scm:git:ssh://github.com/ZiluckMichael/FortuneBlocks.git'
                    url = 'https://github.com/ZiluckMichael/FortuneBlocks'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}